class SigningConfig {
    String storeFile, storePassword, keyAlias, keyPassword, helpEmail, apiBaseUrl

    boolean isValid() {
        return !(storeFile == null || storeFile.isEmpty())
    }

    void clear() {
        storeFile = null
        storePassword = null
        keyAlias = null
        keyPassword = null
        helpEmail = null
        apiBaseUrl = null
    }
}

def config = new SigningConfig()
File signFile = rootProject.file('key.properties')
if (signFile.exists()) {
    signFile.withReader { reader ->
        Properties prop = new Properties()
        prop.load(reader)
        config.storeFile = prop.storeFile
        config.storePassword = prop.storePassword
        config.keyAlias = prop.keyAlias
        config.keyPassword = prop.keyPassword
        config.helpEmail = prop.HELP_E_MAIL
        config.apiBaseUrl = prop.API_BASE_URL
    }

    if (config.isValid()) {
        android {
            defaultConfig {
                buildConfigField 'String', 'HELP_E_MAIL', config.helpEmail
                buildConfigField 'String', 'API_BASE_URL', config.apiBaseUrl
            }
            signingConfigs {
                signingKey {
                    storeFile file(config.storeFile)
                    storePassword config.storePassword
                    keyAlias config.keyAlias
                    keyPassword config.keyPassword
                }
            }
            buildTypes {
                release {
                    signingConfig signingConfigs.signingKey
                }
                debug {
                    signingConfig signingConfigs.signingKey
                }
            }
        }
    }
} else {
    // For Circle CI
    android {
        defaultConfig {
            buildConfigField 'String', 'HELP_E_MAIL', '\"\"'
            buildConfigField 'String', 'API_BASE_URL', '\"\"'
        }
    }
}
