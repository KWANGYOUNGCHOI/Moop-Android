apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.firebase.firebase-perf'

def useReleaseKeystore = rootProject.file("signing/app-release.jks").exists()

android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId build_versions.appId
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.versionCode
        versionName build_versions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("signing/app-debug.jks")
            storePassword "android"
            keyAlias "android"
            keyPassword "android"
        }
        release {
            if (useReleaseKeystore) {
                storeFile rootProject.file("signing/app-release.jks")
                storePassword propOrDef('storePassword', "")
                keyAlias propOrDef('keyAlias', "")
                keyPassword propOrDef('keyPassword', "")
            }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"

            def filesAuthorityValue = 'soup.movie.debug.shareprovider'
            buildConfigField 'String', 'FILES_AUTHORITY', "\"${filesAuthorityValue}\""
            manifestPlaceholders = [
                    filesAuthority     : filesAuthorityValue,
                    crashlytics_enabled: false
            ]
        }
        release {
            if (useReleaseKeystore) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            def filesAuthorityValue = 'soup.movie.shareprovider'
            buildConfigField 'String', 'FILES_AUTHORITY', "\"${filesAuthorityValue}\""
            manifestPlaceholders = [
                    filesAuthority     : filesAuthorityValue,
                    crashlytics_enabled: true
            ]
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dataBinding {
        enabled = true
    }
    androidExtensions {
        features = ["parcelize"]
    }
    dynamicFeatures = [
            ":feature:theatermap"
    ]
}

dependencies {
    implementation project(':feature:home')
    implementation project(':feature:detail')
    implementation project(':feature:search')
    implementation project(':feature:theater')
    implementation project(':feature:settings')
    implementation project(':feature:theme')
    implementation project(':feature:work')
    implementation project(':feature:system')
    implementation project(':data:repository')
    implementation project(':data:db')
    implementation project(':data:api')
    implementation project(':core')

    implementation deps.kotlin.stdlib

    implementation deps.androidx.activity_ktx
    implementation deps.androidx.appcompat
    implementation deps.androidx.core_ktx
    implementation deps.androidx.constraintlayout
    implementation deps.androidx.drawerlayout
    implementation deps.androidx.fragment_ktx
    implementation deps.androidx.preference_ktx
    implementation deps.androidx.recyclerview
    implementation deps.androidx.transition
    implementation deps.androidx.material
    implementation deps.androidx.viewpager2

    implementation deps.insetter.ktx

    implementation deps.lifecycle.extensions
    implementation deps.lifecycle.livedata_ktx
    implementation deps.lifecycle.viewmodel_ktx
    implementation deps.lifecycle.runtime_ktx
    kapt deps.lifecycle.compiler

    implementation deps.navigation.fragment_ktx
    implementation deps.navigation.ui_ktx

    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    kapt deps.dagger.compiler
    kapt deps.dagger.android_processor
    compileOnly deps.dagger.assisted_inject
    kapt deps.dagger.assisted_inject_processor

    implementation deps.glide.runtime
    kapt deps.glide.compiler

    implementation deps.firebase.core
    implementation deps.firebase.crashlytics
    implementation deps.firebase.performance
    implementation deps.firebase.messaging
    implementation deps.firebase.dynamic_links
    implementation deps.firebase.ads

    debugImplementation deps.stetho.core
    implementation deps.util.timber

    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.naver.maps:map-sdk:3.6.2'

    implementation 'com.google.android.play:core-ktx:1.6.4'

    testImplementation deps.test.junit
    androidTestImplementation deps.test.runner
    androidTestImplementation deps.test.espresso
}

if (file("google-services.json").exists()) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'io.fabric'
}
