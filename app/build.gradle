apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId build_versions.appId
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.versionCode
        versionName build_versions.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // API Keys
        buildConfigField 'String', 'K_KEY', "\"" + getKey("kkey") + "\""

        def filesAuthorityValue = applicationId + '.shareprovider'
        buildConfigField 'String', 'FILES_AUTHORITY', "\"${filesAuthorityValue}\""
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dataBinding {
        enabled = true
    }
    androidExtensions {
        experimental = true
    }
}

def getKeyProperties() {
    def properties = new Properties()
    properties.load(project.rootProject.file('key.properties').newDataInputStream())
    return properties
}

def getKey(propertyName) {
    return getKeyProperties().getProperty(propertyName)
}

dependencies {
    implementation project(':data')
    implementation project(':widget')
    implementation project(':animators')

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.kotlin.stdlib

    implementation deps.androidx.appcompat
    implementation deps.androidx.recyclerview
    implementation deps.androidx.cardview
    implementation deps.androidx.palette
    implementation deps.androidx.transition
    implementation deps.androidx.core_ktx
    implementation deps.androidx.constraint_layout
    implementation deps.androidx.material
    implementation deps.androidx.browser

    implementation deps.timber

    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    kapt deps.dagger.compiler
    kapt deps.dagger.android_support_compiler

    implementation deps.room.runtime
    implementation deps.room.rxjava2
    kapt deps.room.compiler

    implementation deps.retrofit.runtime
    implementation deps.retrofit.gson
    implementation deps.retrofit.rxjava2

    implementation deps.rxjava2
    implementation deps.rx_android
    implementation deps.rx_relay

    // UI
    //implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
    implementation deps.glide.runtime
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'

    implementation group: 'com.kakao.sdk', name: 'kakaolink', version: '1.13.0'

    // logging
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.google.firebase:firebase-core:16.0.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'

    testImplementation deps.junit
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.espresso.core
}

apply plugin: 'com.google.gms.google-services'
