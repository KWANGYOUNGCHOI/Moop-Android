apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId build_versions.appId
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.versionCode
        versionName build_versions.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // API Keys
        buildConfigField 'String', 'KOBIS_API_KEY', "\"" + getKobisApiKey() + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    dataBinding {
        enabled = true
    }
}

def getKeyProperties() {
    def properties = new Properties()
    properties.load(project.rootProject.file('key.properties').newDataInputStream())
    return properties
}

def getKobisApiKey() {
    return getKeyProperties().getProperty("KobisApiKey")
}

dependencies {
    implementation project(':data')
    implementation project(':widget')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.kotlin.stdlib

    implementation deps.support.app_compat
    implementation deps.support.recyclerview
    implementation deps.support.cardview
    implementation deps.support.design
    implementation deps.support.palette
    implementation deps.support.transition
    implementation deps.constraint_layout

    implementation deps.timber
    implementation deps.butterknife.runtime
    kapt deps.butterknife.compiler

    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    kapt deps.dagger.compiler
    kapt deps.dagger.android_support_compiler

    implementation deps.retrofit.runtime
    implementation deps.retrofit.gson
    implementation deps.retrofit.rxjava2

    implementation deps.rxjava2
    implementation deps.rx_android
    implementation deps.rx_relay

    // UI
    implementation "com.hannesdorfmann.mosby3:mvi:3.1.0"
    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
    implementation deps.glide.runtime

    // logging
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    testImplementation deps.junit
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.espresso.core
}
