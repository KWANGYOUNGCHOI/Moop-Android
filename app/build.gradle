apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.firebase.firebase-perf'

def useReleaseKeystore = rootProject.file("signing/app-release.jks").exists()

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId build_versions.appId
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.versionCode
        versionName build_versions.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        def filesAuthorityValue = applicationId + '.shareprovider'
        buildConfigField 'String', 'FILES_AUTHORITY', "\"${filesAuthorityValue}\""
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]

        buildConfigField 'String', 'API_BASE_URL', propOrDef("MOOP_API_BASE_URL", "")
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("signing/app-debug.jks")
            storePassword "android"
            keyAlias "android"
            keyPassword "android"
        }
        release {
            if (useReleaseKeystore) {
                storeFile rootProject.file("signing/app-release.jks")
                storePassword propOrDef('storePassword', "")
                keyAlias propOrDef('keyAlias', "")
                keyPassword propOrDef('keyPassword', "")
            }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            manifestPlaceholders = [crashlytics_enabled: false]
        }
        release {
            if (useReleaseKeystore) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            manifestPlaceholders = [crashlytics_enabled: true]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dataBinding {
        enabled = true
    }
    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation project(':data')
    implementation project(':widget')
    implementation project(':theme')

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.kotlin.stdlib

    implementation deps.androidx.appcompat
    implementation deps.androidx.recyclerview
    implementation deps.androidx.transition
    implementation deps.androidx.core_ktx
    implementation deps.androidx.constraint_layout
    implementation deps.androidx.material
    implementation deps.androidx.browser

    implementation deps.lifecycle.extensions
    kapt deps.lifecycle.compiler

    implementation deps.navigation.fragment_ktx
    implementation deps.navigation.ui_ktx

    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    kapt deps.dagger.compiler
    kapt deps.dagger.android_support_compiler

    implementation deps.room.runtime
    implementation deps.room.rxjava2
    kapt deps.room.compiler

    implementation deps.retrofit.runtime
    implementation deps.retrofit.gson
    implementation deps.retrofit.rxjava2

    implementation deps.rx.java2
    implementation deps.rx.kotlin
    implementation deps.rx.android
    implementation deps.rx.relay

    implementation deps.glide.runtime
    kapt deps.glide.compiler

    implementation deps.firebase.core
    implementation deps.firebase.crashlytics
    implementation deps.firebase.performance
    implementation deps.firebase.messaging

    debugImplementation deps.stetho.core
    debugImplementation deps.stetho.okhttp3
    implementation deps.util.okhttp_interceptor
    implementation deps.util.timber
    implementation deps.util.threetenabp

    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'
    implementation 'com.robinhood.ticker:ticker:2.0.0'
    implementation 'com.github.stfalcon:stfalcon-imageviewer:0.1.0'
    implementation 'com.kakao.sdk:kakaolink:1.14.0'

    testImplementation deps.junit
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.espresso.core
}

if (file("google-services.json").exists()) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'io.fabric'
}
