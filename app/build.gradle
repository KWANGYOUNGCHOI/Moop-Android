apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "soup.movie"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // API Keys
        buildConfigField 'String', 'KOBIS_API_KEY', "\"" + getKobisApiKey() + "\""
        buildConfigField 'String', 'MOVIST_API_KEY', "\"" + getMovistApiKey() + "\""
        buildConfigField 'String', 'NAVER_CLIENT_ID', "\"" + getNaverClientID() + "\""
        buildConfigField 'String', 'NAVER_CLIENT_SECRET', "\"" + getNaverClientSecret() + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def getKeyProperties() {
    def properties = new Properties()
    properties.load(project.rootProject.file('key.properties').newDataInputStream())
    return properties;
}

def getKobisApiKey() {
    return getKeyProperties().getProperty("KobisApiKey");
}

def getMovistApiKey() {
    return getKeyProperties().getProperty("MovistApiKey");
}

def getNaverClientID() {
    return getKeyProperties().getProperty("NaverClientID");
}

def getNaverClientSecret() {
    return getKeyProperties().getProperty("NaverClientSecret");
}

ext {
    supportLibraryVersion = '27.1.1'

    MosbyVersion = '3.1.0'
    daggerVersion = '2.13'
    daggerAndroidVersion = '2.13'
    butterknifeVersion = '8.8.1'

    RetrofitVersion = '2.3.0'

    junitVersion = '4.12'
    espressoVersion = '3.0.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // default
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:palette-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:transition:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:1.1.0-beta6"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "org.apache.commons:commons-collections4:4.1"
    implementation "org.apache.commons:commons-lang3:3.7"

    // Mosby
    implementation "com.hannesdorfmann.mosby3:mvi:$MosbyVersion"

    // UI
    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
    implementation 'com.github.bumptech.glide:glide:4.1.1'

    // network
    implementation "com.squareup.retrofit2:retrofit:$RetrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$RetrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$RetrofitVersion"

    // database
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.persistence.room:runtime:1.0.0"
    implementation "android.arch.persistence.room:rxjava2:1.0.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.1.11"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "com.jakewharton.rxrelay2:rxrelay:2.0.0"

    // RxBinding
    implementation "com.jakewharton.rxbinding2:rxbinding:2.1.1"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:2.0.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0"

    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // logging
    implementation 'com.jakewharton.timber:timber:4.6.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4',
            'com.facebook.stetho:stetho:1.5.0',
            'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    // Dagger dependencies
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerAndroidVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerAndroidVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.robolectric:robolectric:3.8-SNAPSHOT"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
