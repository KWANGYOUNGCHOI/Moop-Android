apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId build_versions.appId
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.versionCode
        versionName build_versions.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'HELP_E_MAIL', get('HELP_E_MAIL')
        buildConfigField 'String', 'API_BASE_URL', get('API_BASE_URL')

        def filesAuthorityValue = applicationId + '.shareprovider'
        buildConfigField 'String', 'FILES_AUTHORITY', "\"${filesAuthorityValue}\""
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
    }
    signingConfigs {
        config {
            storeFile file(get("storeFile"))
            storePassword get("storePassword")
            keyAlias get("keyAlias")
            keyPassword get("keyPassword")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dataBinding {
        enabled = true
    }
    androidExtensions {
        experimental = true
    }
}

def getKeyProperties() {
    def properties = new Properties()
    properties.load(project.rootProject.file('key.properties').newDataInputStream())
    return properties
}

def get(propertyName) {
    return getKeyProperties().getProperty(propertyName)
}

dependencies {
    implementation project(':data')
    implementation project(':widget')
    implementation project(':theme')

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.kotlin.stdlib

    implementation deps.androidx.appcompat
    implementation deps.androidx.recyclerview
    implementation deps.androidx.cardview
    implementation deps.androidx.transition
    implementation deps.androidx.core_ktx
    implementation deps.androidx.constraint_layout
    implementation deps.androidx.material
    implementation deps.androidx.browser

    implementation deps.lifecycle.extensions
    kapt deps.lifecycle.compiler

    implementation deps.navigation.fragment_ktx
    implementation deps.navigation.ui_ktx

    implementation deps.timber

    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    kapt deps.dagger.compiler
    kapt deps.dagger.android_support_compiler

    implementation deps.room.runtime
    implementation deps.room.rxjava2
    kapt deps.room.compiler

    implementation deps.retrofit.runtime
    implementation deps.retrofit.gson
    implementation deps.retrofit.rxjava2

    implementation deps.rx.java2
    implementation deps.rx.kotlin
    implementation deps.rx.android
    implementation deps.rx.relay

    // UI
    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'
    implementation 'com.robinhood.ticker:ticker:2.0.0'
    implementation 'com.github.stfalcon:stfalcon-imageviewer:0.1.0'

    implementation deps.glide.runtime
    kapt deps.glide.compiler

    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'

    implementation 'com.kakao.sdk:kakaolink:1.14.0'

    // logging
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation deps.okhttp_logging_interceptor

    implementation deps.firebase.core
    implementation deps.firebase.crashlytics
    implementation deps.firebase.performance
    implementation deps.firebase.messaging

    testImplementation deps.junit
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.espresso.core
}

apply plugin: 'com.google.gms.google-services'
