apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: '../dependencies-variable.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId project.appId
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode project.versionCode
        versionName project.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // API Keys
        buildConfigField 'String', 'KOBIS_API_KEY', "\"" + getKobisApiKey() + "\""
        buildConfigField 'String', 'MOVIST_API_KEY', "\"" + getMovistApiKey() + "\""
        buildConfigField 'String', 'NAVER_CLIENT_ID', "\"" + getNaverClientID() + "\""
        buildConfigField 'String', 'NAVER_CLIENT_SECRET', "\"" + getNaverClientSecret() + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def getKeyProperties() {
    def properties = new Properties()
    properties.load(project.rootProject.file('key.properties').newDataInputStream())
    return properties;
}

def getKobisApiKey() {
    return getKeyProperties().getProperty("KobisApiKey");
}

def getMovistApiKey() {
    return getKeyProperties().getProperty("MovistApiKey");
}

def getNaverClientID() {
    return getKeyProperties().getProperty("NaverClientID");
}

def getNaverClientSecret() {
    return getKeyProperties().getProperty("NaverClientSecret");
}

ext {
    daggerVersion = '2.13'
    daggerAndroidVersion = '2.13'
}

dependencies {
    implementation project(':common')
    implementation project(':data')

    // UI
    implementation "com.hannesdorfmann.mosby3:mvi:3.1.0"
    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
    implementation 'com.github.bumptech.glide:glide:4.1.1'

    // Dagger dependencies
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerAndroidVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerAndroidVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"

    // logging
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4',
            'com.facebook.stetho:stetho:1.5.0',
            'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
}
